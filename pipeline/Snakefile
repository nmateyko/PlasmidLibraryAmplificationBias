configfile: "test_config.yaml"

def get_PE_input_fastqs(wildcards):
  sample_suffix = config["samples"][wildcards.sample]
  data_dir = "/arc/project/st-cdeboer-1/GSC-seq/2022-08-17_Omar_Nick"
  return [f"{data_dir}/HNJKLDSX3_2_1_{sample_suffix}", f"{data_dir}/HNJKLDSX3_2_2_{sample_suffix}"]


rule all:
  input:
    expand("output/counts/{sample}.pkl", sample=config["samples"])


rule cutadapt_trim_PE:
  input:
    get_PE_input_fastqs
  output: 
    r1=temp("output/trimmed/{sample}_1.fastq"),
    r2=temp("output/trimmed/{sample}_2.fastq")
  log:
    summary="output/logs/cutadapt/{sample}_cutadapt.log",
    untrimmed_1="output/logs/cutadapt/{sample}_untrimmed_1.fastq.gz",
    untrimmed_2="output/logs/cutadapt/{sample}_untrimmed_2.fastq.gz",
    short_1="output/logs/cutadapt/{sample}_short_1.fastq.gz",
    short_2="output/logs/cutadapt/{sample}_short_2.fastq.gz",
    long_1="output/logs/cutadapt/{sample}_long_1.fastq.gz",
    long_2="output/logs/cutadapt/{sample}_long_2.fastq.gz",
  conda:
    "environment.yaml"
  params:
    a1="^TGCATTTTTTTCACATC...GGTTACGGCTGTT",
    a2="^AACAGCCGTAACC...GATGTGAAAAAAATGCA",
    min_len=80,
    max_len=80,
  threads: 1
  resources:
    mem_mb=4800,
    walltime="00:20:00"
  shell:
    "cutadapt -j {threads} --untrimmed-o {log.untrimmed_1} --untrimmed-p {log.untrimmed_2} "
    "-m {params.min_len} --too-short-o {log.short_1} --too-short-p {log.short_2} "
    "-M {params.max_len} --too-long-o {log.long_1} --too-long-p {log.long_2} "
    "-a {params.a1} -A {params.a2} -o {output.r1} -p {output.r2} {input} > {log.summary}"


rule ngmerge:
  input:
    r1="output/trimmed/{sample}_1.fastq",
    r2="output/trimmed/{sample}_2.fastq"
  output: 
    temp("output/paired/{sample}.fastq")
  log:
    "output/logs/NGmerge/NGmerge_failed_{sample}"
  conda:
    "environment.yaml"
  threads: 1
  resources:
    mem_mb=4800,
    walltime="00:20:00"
  shell:
    "NGmerge -1 {input.r1} -2 {input.r2} -o {output} -y -f {log} -n {threads}"


rule filter_length:
  input:
    "output/paired/{sample}.fastq"
  output:
    temp("output/length_filtered/{sample}.fastq")
  log:
    too_short="output/logs/length_filter/too_short_{sample}.fastq.gz",
    too_long="output/logs/length_filter/too_long_{sample}.fastq.gz",
  conda:
    "environment.yaml"
  params:
    desired_len=80,
    too_short=79,
    too_long=81,
  threads: 1
  resources:
    mem_mb=4800,
    walltime="00:20:00"
  shell:
    """
    seqkit seq {input} -m {params.desired_len} -M {params.desired_len} -j {threads} -o {output}
    seqkit seq {input} -M {params.too_short} -j {threads} -o {log.too_short}
    seqkit seq {input} -m {params.too_long} -j {threads} -o {log.too_long}
    """
    

rule cluster:
  input:
    "output/length_filtered/{sample}.fastq"
  output:
    temp("output/clustered/{sample}.txt")
  log:
    "output/logs/starcode/{sample}_starcode.log"
  conda:
    "environment.yaml"
  params:
    distance=3
  threads: 1
  resources:
    mem_mb=4800,
    walltime="00:20:00"
  shell:
    "starcode -i {input} -o {output} --print-clusters --seq-id -c -t {threads} -d {params.distance} > {log}"


rule make_counter:
  input:
    "output/length_filtered/{sample}.fastq",
    "output/clustered/{sample}.txt"
  output:
    "output/counts/{sample}.pkl"
  conda:
    "environment.yaml"
  threads: 1
  resources:
    mem_mb=4800,
    walltime="00:20:00"
  script:
    "scripts/starcode_to_counter.py"